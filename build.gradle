plugins {
    id "java"
    id "org.embulk.embulk-plugins" version "0.7.0"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.1.0-SNAPSHOT"

// The root project is a dummy Embulk plugin to be tested with ":embulk-junit5-api" and ":embulk-junit5-engine".
//
// src/main -- The main Embulk plugin code
// src/test -- The "unit tests" for the plugin code (normal JUnit 5 tests without any classpath hacks)
// src/embulkTest -- The "Embulk plugin tests" to run with ":embulk-junit5-api" and ":embulk-junit5-engine"

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()

    // No need to declare "embulkTestImplementation" explicitly as the sourceSet "embulkTest" is declared below.
    // No need to declare "embulkTestRuntime" explicitly as the sourceSet "embulkTest" is declared below.
    //
    // "embulkTestImplementation" should not ".extendsFrom testImplementation"
    // so that it won't include main and test in the top-level class loader.
    //
    // "embulkTestRuntime" should not ".extendsFrom testRuntime" similarly.
}

sourceSets {
    embulkTest {
        java {
            srcDir file("src/embulkTest/java")
        }
        resources.srcDir file("src/embulkTest/resources")
    }
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"
    compileOnly "org.slf4j:slf4j-api:2.0.7"

    implementation "org.embulk:embulk-util-config:0.5.0"
    implementation "javax.validation:validation-api:2.0.1.Final"

    implementation platform("com.fasterxml.jackson:jackson-bom:2.16.2")
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.0"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.0"

    embulkTestImplementation project(":embulk-junit5-api")  // TODO: embulk-junit5-api depends on junit-jupiter-api transitively?
    embulkTestImplementation "org.junit.jupiter:junit-jupiter-api:5.11.0"

    embulkTestRuntimeOnly project(":embulk-junit5-engine")

    // embulk-core can be loaded in the top-level class loader.
    embulkTestRuntimeOnly "org.embulk:embulk-core:0.11.5"
}

embulkPlugin {
    mainClass = "org.embulk.input.junit5example.ExampleInputPlugin"
    category = "input"
    type = "junit5example"
}

test {
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

task embulkTest(type: Test) {
    doFirst {
        println "^^^^^ Classpath information in main ^^^^^"
        println sourceSets.main.runtimeClasspath
        println sourceSets.main.runtimeClasspathConfigurationName
        sourceSets.main.runtimeClasspath.each {
            println it
        }
        println "^^^^^ Classpath information in embulkTest ^^^^^"
        println sourceSets.embulkTest.runtimeClasspath
        println sourceSets.embulkTest.runtimeClasspathConfigurationName
        sourceSets.embulkTest.runtimeClasspath.each {
            println it
        }
        println "^^^^^ Classpath information done ^^^^^"
    }

    useJUnitPlatform()

    // TODO See the following Gradle implementations to investigate Gradle classpath configurations.
    //
    // JavaPlugin.createDefaultTestSuite
    // https://github.com/gradle/gradle/blob/v8.10.1/platforms/jvm/plugins-java/src/main/java/org/gradle/api/plugins/JavaPlugin.java#L354-L383
    //
    // SourceSet.getRuntimeClasspathConfigurationName
    // https://github.com/gradle/gradle/blob/v8.10.1/platforms/jvm/platform-jvm/src/main/java/org/gradle/api/internal/tasks/DefaultSourceSet.java#L194-L197
    //
    // SourceSet.getRuntimeClasspath
    // https://github.com/gradle/gradle/blob/v8.10.1/platforms/jvm/platform-jvm/src/main/java/org/gradle/api/internal/tasks/DefaultSourceSet.java#L239-L242
    classpath = sourceSets.embulkTest.runtimeClasspath

    // Pick up "@EmbulkPluginTest" methods from the classes in "testClassesDirs"
    testClassesDirs = sourceSets.embulkTest.output.classesDirs

    testLogging {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}
